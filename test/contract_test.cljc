(ns contract-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.physics.contract :as contract]))

(defrecord StubBackend []
  contract/PhysicsBackend
  (descriptor [_] {:id :stub :fidelity :realtime :capabilities #{:rigid-body-2d :collision-layers}})
  (step [_ scene dt] (assoc scene :stepped-by dt))
  (solve [_ _] (throw (ex-info "not a finite solver" {}))))

(deftest shared-scene-and-backend-contract
  (let [s (contract/make-scene {:id :world :dimensions 2 :entities []})
        backend (->StubBackend)]
    (is (= contract/si-units (:scene/units s)))
    (is (contract/supports? backend :realtime #{:rigid-body-2d}))
    (is (not (contract/supports? backend :high-fidelity #{:rigid-body-2d})))
    (is (= 0.016 (:stepped-by (contract/step backend s 0.016))))))

(deftest finite-case-cannot-imply-qualification
  (let [s (contract/make-scene {:id :wind-tunnel :dimensions 3 :entities []})
        c (contract/make-case {:id :cfd-1 :scene s :domain :cfd :backend-kind :openfoam
                          :fidelity :high-fidelity :controls {}})
        r (contract/make-result {:case-id (:case/id c) :backend :openfoam :status :completed
                            :fields {:drag 1.2} :qualification {:execution :qualified
                                                               :experimental-validation :rejected}
                            :evidence []})]
    (testing "execution success does not become experimental qualification"
      (is (contract/qualified? r :execution))
      (is (not (contract/qualified? r :experimental-validation))))))
