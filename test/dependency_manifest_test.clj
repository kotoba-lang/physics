(ns dependency-manifest-test
  (:require [clojure.edn :as edn]
            [clojure.java.io :as io]
            [clojure.test :refer [deftest is testing]]))

(defn manifest []
  (-> "kotoba/physics/vehicle-stack.edn" io/resource slurp edn/read-string))

(defn- dependency-lib [modules dependency-id]
  (get-in modules [dependency-id :lib]))

(defn- module-deps-file [{:workspace/keys [path]}]
  (io/file (System/getProperty "user.dir") path "deps.edn"))

(deftest vehicle-stack-is-a-closed-edn-dependency-graph
  (let [{:keys [stack/id stack/version modules selection policy]} (manifest)
        module-ids (set (keys modules))
        dependencies (mapcat :depends-on (vals modules))]
    (is (= :kotoba/vehicle-physics id))
    (is (= 1 version))
    (is (every? module-ids dependencies))
    (is (= :kotoba/kami-vehicle (get-in selection [:realtime :backend])))
    (is (= :kotoba/vehicle-road-load-rom (get-in selection [:design :backend])))
    (is (true? (:direct-require-must-be-direct-dependency policy)))
    (testing "the two executable backends share the contract module"
      (is (contains? (set (get-in modules [:realtime :depends-on])) :physics))
      (is (contains? (set (get-in modules [:rom :depends-on])) :physics)))))

(deftest checked-out-modules-declare-every-direct-edge
  (let [{:keys [modules]} (manifest)
        checked (atom 0)]
    (doseq [[module-id module] modules
            :let [deps-file (module-deps-file module)]
            :when (.isFile deps-file)]
      (swap! checked inc)
      (let [declared (-> deps-file slurp edn/read-string :deps keys set)]
        (doseq [dependency-id (:depends-on module)]
          (is (contains? declared (dependency-lib modules dependency-id))
              (str module-id " must directly declare " dependency-id
                   " in " deps-file)))))
    (is (pos? @checked) "at least the physics module must be checked")))
